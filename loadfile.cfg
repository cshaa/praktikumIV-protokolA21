# -------------------------------
# loadfile.gnuplot
# version 1.0
# license CC0
# -------------------------------
# This is a gnuplot subroutine for reading a file into an array
# -------------------------------
# Expected arguments:
# LF_File - name of the file to be loaded
# LF_Columns - number of columns to load
# -------------------------------
# Output:
# LF_Rows - number of rows loaded
# LF_Col1 - contents of the first col as an array
# LF_Col2 - contents of the second col
# LF_Col3 - etc.
# -------------------------------


LF__prev_output = GPVAL_OUTPUT
set term push
set term dumb
set output '/dev/null'
set macros

LF_Rows = 0
if (!exists('LF_Columns')) LF_Columns = 2
if (!exists('LF_File')) LF_File = 'data1'


# Count rows
plot LF_File using ( LF_Rows = LF_Rows + 1, $1 ):2

# Make arrays
LF__m = ''
do for [LF__i = 1:LF_Columns] {
    LF__m = LF__m.'; array LF_Col'.LF__i.'[LF_Rows]'
}
@LF__m

# Prepare write macro
LF__m = 'LF__i = LF__i + 1'
do for [LF__i = 1:LF_Columns] {
    LF__m = LF__m.', LF_Col'.LF__i.'[LF__i] = stringcolumn('.LF__i.')'
}

# Fill arrays
LF__i = 0
plot LF_File using ( @LF__m, $1 ):2

set term pop
if (LF__prev_output eq "") {
    set output
} else {
    set output LF__prev_output
}